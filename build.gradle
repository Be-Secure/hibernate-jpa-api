buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        mavenRepo name: 'jboss-nexus', url: "https://repository.jboss.org/nexus/content/groups/public/"
    }
    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-upload-auth-plugin:1.1.1'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'uploadAuth'

repositories {
    // needed to resolve the maven webdav wagon provider
    mavenCentral()
}

configurations {
    // classpath config used for maven deployers (wagon providers)
    deployerJars
}

dependencies {
    deployerJars 'org.apache.maven.wagon:wagon-webdav:1.0-beta-2'
}

group = 'org.hibernate.javax.persistence'
version = '1.0.0-SNAPSHOT'

// gradle uses 'build/' by default
buildDirName = "target"

manifest.mainAttributes(
        provider: 'gradle',
        'Built-By': 'Hibernate.org',
        'Specification-Title': 'Java Persistence API, Version 2.1',
        'Specification-Version': '2.1',
        'Specification-Vendor': 'Oracle, Inc.',
        'Implementation-Version': version,
        'Implementation-Vendor': 'hibernate.org',
        'Implementation-Title': 'Java Persistence API'
)

// create the javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    // add the javadoc jar to the archives configuration to get it uploaded properly
    archives javadocJar
}

uploadArchives {
    repositories {
        deployer = mavenDeployer {
            name = 'jbossDeployer'
            configuration = configurations.deployerJars
            repository(id: "jboss-releases-repository", url: "https://repository.jboss.org/nexus/service/local/staging/deploy/maven2/")
            snapshotRepository(id: "jboss-snapshots-repository", url: "https://repository.jboss.org/nexus/content/repositories/snapshots")
            pom.project {
                licenses {
                    license {
                        name 'Eclipse Public License (EPL), Version 1.0'
                        url 'http://www.eclipse.org/legal/epl-v10.html'
                        distribution 'repo'
                    }
                    license {
                        name 'Eclipse Distribution License (EDL), Version 1.0'
                        url 'http://www.eclipse.org/org/documents/edl-v10.php'
                    }
                }
            }
        }
    }
}
